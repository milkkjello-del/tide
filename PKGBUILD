# Maintainer: tide maintainer <you@example.com>
pkgname=tide
pkgver=1.1.0
pkgrel=1
pkgdesc="A brutalist YouTube Music desktop client"
arch=('any')
url="https://github.com/milkkjello-del/tide"
license=('GPL-3.0-or-later')
depends=(
  'python>=3.12'
  'pyside6'
  'python-mpv'
  'mpv'
  'yt-dlp'
  'python-ytmusicapi'
  'python-cryptography'
  'python-numpy'
  'python-sounddevice'
  'ttf-ibm-plex'
)
optdepends=(
  'python-pypresence: Discord rich presence integration'
  'python-secretstorage: GNOME/libsecret backend for cookie import'
  'kwallet: KDE wallet backend for cookie import'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-hatchling'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('eda71c193c8949bfebf6b00b9a2fd4583d86bd3fc2ac58be54ec6b5d3ee0d199')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
